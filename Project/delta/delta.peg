comments = r'[/][/].*|[/][*](.|\n)*?[*][/]'
identifier = r'[a-zA-Z]\w*'
program = statement* expression EOF
statement = declaration / assignment / if / while
declaration = 'var' decl_variable (',' decl_variable)* ';'
decl_variable = identifier
assignment = lhs_variable '=' expression ';'
if = 'if' expression block ( 'else' block )?
while = 'while' expression block
block = '{' statement* '}'
lhs_variable = identifier
expression = additive ('&&' additive)*
additive = multiplicative (('+' / '-') multiplicative)*
multiplicative = unary (('*' / '/' / '%') unary)*
unary = ('+' / '-' / '!')* primary
primary = integer / boolean / parenthesis / rhs_variable
integer = decimal / binary / octal / hexadecimal
decimal = r'\d+'
binary = r'#b[01]+'
octal = r'#o[0-7]+'
hexadecimal = r'#x[0-9a-fA-F]+'
boolean = 'true' / 'false'
parenthesis = '(' expression ')'
rhs_variable = identifier